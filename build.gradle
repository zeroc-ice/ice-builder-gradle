// **********************************************************************
//
// Copyright (c) 2003-2015 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile gradleApi()
    compile localGroovy()
}

sourceSets {
    main {
        groovy {
            srcDir "src/main/groovy"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}

def pluginVersion  = "1.0"

def pomName = "build/libs/ice-gradle-plugin-${pluginVersion}.pom"
task writeNewPom {
    outputs.file file(pomName)
} << {
    pom {
        project {
            url 'https://www.zeroc.com'
            packaging 'jar'
            groupId 'com.zeroc.gradle.plugins'
            artifactId 'ice-gradle-plugin'
            version pluginVersion
            // TODO: Extend with licensing information.
        }
    }.writeTo(pomName)
}
jar.dependsOn(writeNewPom)

jar {
    archiveName = "ice-gradle-plugin-${pluginVersion}.jar"
}

task jarSources(type:Jar, dependsOn: jar){
    from sourceSets.main.allSource
    classifier = "${pluginVersion}-source"
}
assemble.dependsOn(jarSources)

//
// Used to build the gradle wrapper to automatically download and install
// the version of gradle needed to build plugin.
//
task wrapper(type: Wrapper) {
    gradleVersion = "2.3"
}
